version = '0.0.2-SNAPSHOT'

apply plugin: 'groovy'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'spring-cloud-contract'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springcloud_version}"
	}
}

dependencies {
	implementation("org.springframework.boot:spring-boot-starter-web")
	implementation("org.springframework.boot:spring-boot-starter-actuator")
	implementation("org.springframework.cloud:spring-cloud-starter-stream-rabbit")

	testImplementation("org.springframework.cloud:spring-cloud-stream-test-support")
	testImplementation("org.springframework.cloud:spring-cloud-starter-contract-verifier")
}

contracts {
	packageWithBaseClasses = 'com.powple.poc'
	baseClassMappings {
		baseClassMapping(".*intoxication.*", "com.powple.poc.intoxication.BeerIntoxicationBase")
	}

	//for apiCompatibility
	if (gradle.startParameter.taskRequests.any { it.args.contains("apiCompatibility") }) {
		contractsPath = "/"
		contractDependency {
			groupId = project.group
			artifactId = project.name
			delegate.classifier = "stubs"
			delegate.version = getProp("latestProductionVersion")
		}
		// The JAR with contracts should be taken from Maven local
		setContractsMode("LOCAL")
		// Or
		/*setContractsMode("REMOTE")
		contractRepository {
			url = getProp('REPO_WITH_JARS') ?:
				getProp('REPO_WITH_BINARIES') ?: 'http://localhost:8081/artifactory/libs-release-local'
		}*/
	}
}

test {
	testLogging {
		exceptionFormat = 'full'
	}
}

task apiCompatibility(type: Test) {
	description = "Task to run api compatbility tests"
	testLogging {
		exceptionFormat = 'full'
	}
	include '**/contracts/**'
}

String getProp(String propName) {
	return hasProperty(propName) ?
		(getProperty(propName) ?: System.properties[propName]) : System.properties[propName] ?:
		System.getenv(propName)
}
